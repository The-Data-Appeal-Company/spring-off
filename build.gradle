buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
}

plugins {
    id 'java'
    id 'jacoco'
    id "maven-publish"
    id "com.jfrog.bintray" version "1.8.4"
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

ext {
    version = "0.7"
}

def pomConfig = {
    scm {
        url "https://github.com/The-Data-Appeal-Company/spring-off"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            groupId 'io.datappeal.spring'
            artifactId 'spring-off'
            version "0.7"
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'spring graceful shutdown on k8s')
                root.appendNode('name', 'spring-off')
                root.appendNode('url', 'https://github.com/thombergs/myAwesomeLib')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = project.hasProperty('bt_user') ? project.property('bt_user') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bt_apikey') ? project.property('bt_apikey') : System.getenv('BINTRAY_APIKEY')
    publish = true
    publications = ['mavenPublication']
    pkg {
        repo = "datappeal"
        name = 'spring-off'
        userOrg = 'datappeal'
        licenses = ['GPL-3.0-or-later']
        labels = ['spring', 'graceful-shutdown', "kubernetes"]
        vcsUrl = 'https://github.com/the-data-appeal-company/spring-off.git'
        version "0.7"
    }
}


group 'io.datappeal.spring.off'
version ext.version

sourceCompatibility = 1.8

dependencies {
    compileOnly group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '2.2.4.RELEASE'

    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '2.2.4.RELEASE'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.15.0'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.2.4'

    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.2.4.RELEASE'
    testImplementation group: 'org.springframework', name: 'spring-web', version: '5.2.3.RELEASE'

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.3.1"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.3.1"
}

test {
    useJUnitPlatform()
}

jacoco {
    toolVersion = "0.8.5"
    reportsDir = file("$buildDir/jacoco")
}
jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

